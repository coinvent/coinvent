%% A 'chord' has exactly one base, exactly one mode, and several optional extensions. 
%% A generalization of 'chord' is 'simultaneity', which does not require exactly one base and exactly one mode. 
%% Generalization of theories always happens by relaxing constructors and sorts, not by removing axioms. Hence, in order to blend two chords, we can not have the generic 'chord' with an unknown base and an unknown mode as generalization space, because there is no relaxation operation that removes axioms. Therefore, we have to generalize to 'simultaneity'. (Relaxation of constructors might also mean to shift a constructor one level higher in the sub-constructor hierarchy. For example, 'lives-in' becomes 'uses'.)


spec Symbols = 
    free type Note ::= c | d | e | f | g | a | b
    free type ModeSymb ::= min | maj | dim | aug
    free type ExtSymb ::= six | seven | nine | eleven | thirteen
end


spec Simultaneity = Symbols then
    sorts Base Mode Ext
    ops isNote : Base -> Note
        isMode : Mode -> ModeSymb
        isExt : Ext -> ExtSymb
end

 
spec ChordsHaveUniqueBaseAndMode = Simultaneity
then
    . exists! b : Base . true
    . exists! m : Mode . true
end


spec Chord = 
Simultaneity then
    ops 
        base : Base
        mode : Mode
end


spec CMaj7Chord = 
Chord then
    ops 
        ext : Ext
        
    . isNote(base) = c
    . isMode(mode) = maj
    . isExt(ext) = seven
end


spec DMin6Chord = 
Chord then
    ops 
        ext : Ext
        
    . isNote(base) = d
    . isMode(mode) = min
    . isExt(ext) = six
end


view GToCMaj7 : Simultaneity to CMaj7Chord end
view GToDMin6 : Simultaneity to DMin6Chord end


spec Naive_Blend_Composition = 
combine GToCMaj7, GToDMin6
end

%% The Naive Blend is inconsistent
spec Naive_Blend_Completion = 
Naive_Blend_Composition and ChordsHaveUniqueBaseAndMode
end   


spec Maj7Simultaneity = 
Simultaneity then
    ops 
        mode : Mode
        ext : Ext
 
    . isMode(mode) = maj
    . isExt(ext) = seven
end


spec D6Simultaneity = 
Simultaneity then
    ops 
        base : Base
        ext : Ext
        
    . isNote(base) = d
    . isExt(ext) = six
end


view D6ChordToDMin6Chord :  D6Simultaneity to DMin6Chord
view Maj7ChordToCMaj7Chord : Maj7Simultaneity to CMaj7Chord


view GToD6Chord : Simultaneity to D6Simultaneity
view GToMaj7Chord : Simultaneity to Maj7Simultaneity


spec Relaxed_Blend_Composition = 
combine GToD6Chord,GToMaj7Chord
end

spec Relaxed_Blend_Completion = 
Relaxed_Blend_Composition and  ChordsHaveUniqueBaseAndMode
end
